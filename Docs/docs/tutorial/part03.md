# 第3章 リストとタプル

## リスト
リストは`[1, 2, 3]`のように同じ型の値を並べたデータ構造です。
Mokkosuはリストを簡単に扱える仕組みを用意しています。

リストは`[1, 2, 3]`のように`[`と`]の間に要素を並べることで
構築できます。

リストから要素を取り出すには`nth`関数を使います。

以下のプログラムでは整数のリストを構築してその2番目(先頭は0)の要素を
取り出して表示しています。

```
let list = [1, 2, 3, 4, 5];
println (nth list 2);
```

実行すると以下のように表示されます。

```
list : [Int]

3
```

リストの型は、要素の型が`α`のとき`[α]`と表します。

二つのリストを連結するには`++`演算子を、
リストの先頭に要素を追加するときは`::`演算子を使います。

上の式を実行すると、偶数の要素だけ残したリスト、すなわち`[2,4]`が得られます。

## パターンマッチ
パターンマッチを使うとリストを分解することができます。

以下のプログラムは、リストのパターンマッチを使って、
リストの全要素を2倍するプログラムの例です。

```
fun double_list list =
  match list {
    [] -> [];
    x :: xs -> x * 2 :: double_list xs;
  };
```

`{`と`}`の間で場合分けを記述します。
まず、空リスト`[]`の場合は空リスト`[]`を返しています。
次にリストを先頭要素`x`と先頭以外の要素`xs`に分解できる場合は、
`x * 2 :: double_list xs`を計算します。
自分自身を再帰的に呼び出すことで、リストの全要素を2倍します。
`double_list`の型は以下のようになります。

```
double_list : [Int] -> [Int]
```

### 練習問題 3.1


## 多相型
以下はリストの全要素を表示する関数の定義例です。
```
fun print_list list =
  match list {
    [] -> ();
    x :: xs -> do println x in print_list xs;
  };
```

この関数の型は以下のようになります。
```
print_list : [α] -> ()
```

この関数は任意のリスト型に関して実行することができます。
ただし、`println`関数の制限から、整数、浮動小数点数、文字列、
文字の場合しか正しく表示されません。
以下は`print_list [1, 2, 3]`の実行例です。

```
1
2
3
```

### 練習問題 3.2
リストを反転する関数を定義してください。

## for式
リスト操作を行う際は、for式を使うと便利です。
以下はfor式を使ってリストのすべての要素を2倍するプログラムの例です。
```
let list = for x <- [1, 2, 3] in x * x;
print_list list;
```
実行すると以下のように表示されます。

```
2
4
6
```

for式にif節を追加すると、
条件に合致したもののみ結果のリストに残すことができます。
```
let list =
  for x <- [1, 2, 3, 4, 5];
      if x % 2 == 0
  in x;
print_list list;
```
実行すると以下のように表示されます。
```
2
4
```

forは以下のように複数のリストに対して使うこともできます。
```
let list =
  for x <- [10, 20];
      y <- [1, 2, 3];
  in x + y;
print_list list;
```
実行すると以下のように表示されます。
```
11
12
13
21
22
23
```

### 練習問題 3.3
整数リストから2の倍数または3の倍数の要素のみを抽出する関数を定義してください。

## タプル
複数の異なる型の値を組にしたものをタプルとよびます。
例えば`1`と`"abc"`を組にした`(1, "abc")`は`(Int, String)`型のタプルです。

タプルを構築するには括弧の間に値をコンマで区切って指定します。
タプルを分解するにはパターンマッチを使います。

以下は2要素タプルの左右の要素を入れ替える関数の定義です。
```
fun swap tuple =
  match tuple {
    (x, y) -> (y, x)
  };
```
関数の型は以下のようになります。
```
swap : (α, β) -> (β, α)
```

## 連想リスト
リストとタプルの重要な応用に連想リストがあります。
連想リストは、
```
let alist = [ ("a", 12), ("b", 5), ("c", 62) ];
```
のようなリストで、キーと値のペアをリストにしたものです。

以下はこの連想リストからキーを指定して値を探索する関数です。
```
fun lookup key alist =
  match alist {
    [] -> error "Not found";
    (x, v) :: rest ->
      if x == key -> v
      else lookup key rest;
  };
```
例えば`lookup "b" alist`を実行すると`5`が返ってきます。

### 練習問題 3.4
リストをソートする関数を書いてください。
